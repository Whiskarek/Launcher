plugins {
    id "io.gitlab.arturbosch.detekt" version "1.0.0-RC12"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply from: 'DeployAppCenter.gradle'
apply from: 'StaticAnalysis.gradle'

import myapp.gradle.PropertiesImporter

def propertiesImporter =
        new PropertiesImporter(rootProject.file("keystore/keystore.properties").path, rootProject)

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "by.whiskarek.yandexlauncher"
        minSdkVersion 23
        targetSdkVersion 28
        compileSdkVersion 28
        versionCode 2
        versionName "1.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        def fabricKey = propertiesImporter.getFabricKey()
        def appCenterKey = propertiesImporter.getAppCenterKey()
        manifestPlaceholders = [fabricKey:fabricKey]
        buildConfigField("String", "APPCENTER_KEY", "\"$appCenterKey\"")
    }
    if (propertiesImporter.canMakeSigning()) {
        signingConfigs {
            release {
                storeFile new File(propertiesImporter.getKeystoreFilePath())
                storePassword propertiesImporter.getKeystorePassword()
                keyPassword propertiesImporter.getKeyPassword()
                keyAlias propertiesImporter.getKeyAlias()
                v1SigningEnabled true
                v2SigningEnabled true
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (propertiesImporter.canMakeSigning()) {
                signingConfig signingConfigs.release
                ext.betaDistributionGroupAliases = 'andrew-shkrob-1'
                def path = "release-notes/${defaultConfig.versionName}.md"
                ext.betaDistributionReleaseNotesFilePath = rootProject.file(path).getPath()
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.core:core-ktx:1.1.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    // Fabric
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.8@aar') {
        transitive = true
    }
    // App Center
    def appCenterSdkVersion = '1.11.2'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}